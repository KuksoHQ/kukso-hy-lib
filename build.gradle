plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

import org.gradle.internal.os.OperatingSystem

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    }
    else {
        def os = OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        }
        else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        }
        else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

if (!project.hasProperty('hytaleHome')) {
    throw new GradleException('Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.');
}
else if (!file(project.findProperty('hytaleHome')).exists()) {
    throw new GradleException("Failed to find Hytale at the expected location. Please make sure you have installed the game. The expected location can be changed using the hytale_home property. Currently looking in ${project.findProperty('hytaleHome')}")
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

description = mod_description

def versionFile = file("version.txt")
if (versionFile.exists()) {
    version = versionFile.text.trim()
} else {
    versionFile.text = project.version?.toString() ?: "0.0.0"
    version = versionFile.text.trim()
}
println "[KuksoHyLib] Using project.version = ${version}"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri('https://nexus.kukso.com/repository/maven-public')
    }
}

dependencies {
    //implementation files('libs/HytaleServer.jar')
    implementation(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

def createServerRunArguments(String srcDir) {
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/$patchline/package/game/latest/Assets.zip" + '"'
    def modPaths = []
    if (includes_pack.toBoolean()) {
        modPaths << srcDir
    }
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    if (!modPaths.isEmpty()) {
        programParameters += ' --mods="' + modPaths.join(',') + '"'
    }
    return programParameters
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
        workingDirectory = serverRunDir.absolutePath
    }
}

// Creates a launch.json file for VSCode with the same configuration
tasks.register('generateVSCodeLaunch') {
    def vscodeDir = file("$projectDir/.vscode")
    def launchFile = file("$vscodeDir/launch.json")
    doLast {
        if (!vscodeDir.exists()) {
            vscodeDir.mkdirs()
        }
        def programParams = createServerRunArguments("\${workspaceFolder}")
        def launchConfig = [
                version: "0.2.0",
                configurations: [
                        [
                                type: "java",
                                name: "HytaleServer",
                                request: "launch",
                                mainClass: "com.hypixel.hytale.Main",
                                args: programParams,
                                cwd: "\${workspaceFolder}/run"
                        ]
                ]
        ]
        launchFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(launchConfig))
    }
}

// Makes sure the plugin manifest is up to date.
processResources {
    def props = [
            mod_description: mod_description,
            website: website,
            server_version: server_version,
            entry_point: entry_point,
            includes_pack: includes_pack,
            mod_name: mod_name,
            group: project.group,
            version: project.version
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('manifest.json') {
        expand(props)
    }

    doLast {
        fileTree(dir: outputs.files.asPath, include: 'manifest.json').each { file ->
            def text = file.text
            text = text.replace('"IncludesAssetPack": "true"', '"IncludesAssetPack": true')
            text = text.replace('"IncludesAssetPack": "false"', '"IncludesAssetPack": false')
            file.text = text
        }
    }
}

tasks.register("printJdk") {
    doLast {
        println "Running with: " + System.getProperty("java.version")
    }
}

jar {
    archiveBaseName.set(mod_name)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name.set(mod_name) //("KuksoHyLib")
                description.set("Core Library for Kukso Hytale Mods")
                url.set("https://github.com/KuksoHQ/kukso-hy-lib")

                licenses {
                    license {
                        name.set("Apache License, Version 2.0")
                        url.set("https://www.apache.org/licenses/LICENSE-2.0")
                    }
                }

                developers {
                    developer {
                        id.set("DevBD1")
                        name.set("M. Burak Dorman")
                        email.set("burakdorman@gmail.com")
                    }
                }

                scm {
                    connection.set("scm:git:git://github.com/KuksoHQ/kukso-hy-lib.git")
                    developerConnection.set("scm:git:ssh://github.com/KuksoHQ/kukso-hy-lib.git")
                    url.set("https://github.com/KuksoHQ/kukso-hy-lib")
                }
            }
        }
    }

    repositories {
//        maven {
//            name = "OSSRH"
//            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
//
//            credentials {
//                username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
//                password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
//            }
//        }
        maven {
            def releaseRepoUrl = uri('https://nexus.kukso.com/repository/maven-releases/')
            def snapshotsRepoUrl = uri('https://nexus.kukso.com/repository/maven-snapshots/')

            name = "KuksoStudios"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releaseRepoUrl

            credentials {
                username = project.findProperty("kuksoUser") ?: System.getenv("KUKSO_USER")
                password = project.findProperty("kuksoToken") ?: System.getenv("KUKSO_TOKEN")
            }
        }
    }
}

signing {
    def keyFile = project.findProperty("signing.keyFile")

    def signingKeyId = project.findProperty("signing.keyId") ?: System.getenv("SIGNING_KEY_ID")
    def signingKey = keyFile ? file(keyFile).text : System.getenv("SIGNING_KEY")
    def signingPassword = project.findProperty("signing.password") ?: System.getenv("SIGNING_PASSWORD")

    if (keyFile && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword as String)
        sign publishing.publications.mavenJava
        logger.lifecycle("[KuksoHyLib] Signing configured using key file: ${keyFile}")
    } else {
        logger.warn("[KuksoHyLib] Signing skipped â€” missing signing.keyFile or signing.password.")
    }
}